## NF-kB model from Sanjana's PLOS comp bio paper
## Reactions from Pekalski et al. (Lipniacki) and Lee et al and Ashall et al (as noted by Sanjana) 
## this is also where some of the parameter values came from, she notes that they are all fitted

begin model

begin parameters 

    ## got these values from Sanjana's implementation of nfkb_lasso.bngl
    # all parameters are log values

    ## Activation Module
    # these parameter values came from Pekalski et al. (Lipniacki) (as noted by Sanjana in her implementation)
    k_b     0.0352      # activation of TNFR, unit: s^-1 (ng/ml)^-1
    k_f     -3.0688     # inactivation of TNFR, unit: s^-1
    k_a     -4.5164     # activation of IKK, unit: s^-1
    k_4     -7.4173     # inactivation of IKK, unit: s^-1
    # these parameter values came from Lee at al (Fold Change paper) (as noted by Sanjana) -- Lee cites Ashall et al., 2009 for these values
    k_i1    -2.057      # shuttling NF-kB, unit: s^-1 
    k_e1    -5.9334     # shuttling NF-kB, unit: s^-1 
    k_t2a   -8          # cytoplasmic bound IkB degradation mediated by IKK, unit: per molecule per second
    k_t1a   -7.3178     # cytoplasmic free IkB degradation mediated by IKK, unit: per molecule per second

    ## IkB Module
    k_i2    -4.1089     # shuttling IkB, unit: s^-1
    k_e2    -10.6289    # shuttling IkB, unit: s^-1
    k_e2a   -3.5859     # exporting NF-kB-IkB from nucleus, unit: s^-1
    c_4a    -4.6363     # basal cytoplasmic free IkB degradation, unit: s^-1
    c_5a    -4.6        # basal cytoplasmic bound IkB degradation, unit: s^-1
    c_1a    -3.2082     # IkB linear production, unit: per molecule per second 
    k_a1a   -7          # NF-kB-IkB formation (reversible nuclear), unit: per molecule per second
    k_d1a   -8          # NF-kB-IkB formation (reversible nuclear), unit: per second

    ## A20 Module
    c_3     -20         # degradation of A20
    c_1     -20         # A20 linear production 
    k_ikk   -20         # inactivation of IKK mediated by A20
    k_tnfr  -20         # inactivation of tnfr mediated by A20

    ## initial conditions --- from Sanjana's implementation 
    TNFRin  2.5258      # total inactive TNFR protein concentration
    IKKin   4.7454      # total inactive IKK protein concentration 
    boundc  6.5546      # total cytoplasmic NFkB-IkB protein concentration
    
end parameters
 
begin molecule types  

    ## activation module
    TNF() # TNF: tumor necrosis factor               

    TNFR(st~i~a) # TNFR: tumor necrosis factor receptor 
                 # st: activation state, i: inactive, a: active 
                 # influenced by TNF and A20        

    IKK(s~I~A) # IKK: IkB Kinase Complex
               # st: activation state, i: inactive, a: active 
               # influenced by TNFRa and A20        

    NFkB(IkB,loc~c~n) # NF-kB: nuclear factor kappa B, can bind to IkB, _act: activation module
                      # loc: location, c: cytoplasmic, n: nuclear
                      # cNFkB influenced by IKK
                      # can be bound or unbound
                        
    ## IkB module 
    IkB(NFkB,loc~c~n) # IkB: IkappaB Kinase, can bind to NFkB
                      # loc: location, c: cytoplasmic, n: nuclear
                      # can be bound or unbound
   
    ## A20 module
    A20()    

end molecule types

begin seed species 

    ## activation module
    TNF()                   0 
    TNFR(st~i)              10^TNFRin
    TNFR(st~a)              0 
    IKK(s~I)                10^IKKin         
    IKK(s~A)                0
    NFkB(IkB,loc~c)         0
    NFkB(IkB,loc~n)         0
    ## IkB module 
    IkB(NFkB,loc~c)        0
    IkB(NFkB,loc~n)        0
    # bound NF-kB-IkB
    NFkB(IkB!0,loc~c).IkB(NFkB!0,loc~c) 10^boundc
    NFkB(IkB!0,loc~n).IkB(NFkB!0,loc~n) 0
    ## A20 module
    A20()                   0  

end seed species

begin observables 

    # activation module
    Molecules TNF TNF()
    Molecules TNFRi TNFR(st~i)
    Molecules TNFRa TNFR(st~a)
    Molecules IKKi IKK(s~I)
    Molecules IKKa IKK(s~A)
    Molecules cNFkB NFkB(IkB,loc~c)
    Molecules nNFkB NFkB(IkB,loc~n)

    # IkB module
    Molecules cIkB IkB(NFkB,loc~c)
    Molecules nIkB IkB(NFkB,loc~n)
    Species cNFkB_IkB NFkB(IkB!0,loc~c).IkB(NFkB!0,loc~c)
    Species nNFkB_IkB NFkB(IkB!0,loc~n).IkB(NFkB!0,loc~n)

    # A20 module
    Molecules A20 A20()
    
end observables

begin reaction rules 

    # activation of TNFR by TNF
    R1: TNFR(st~i) + TNF() -> TNFR(st~a) + TNF()    10^k_b
    # inactivation of TNFR 
    R2: TNFR(st~a) -> TNFR(st~i)    10^k_f
    # activation of IKK by TNFRa
    R3: IKK(s~I) + TNFR(st~a) -> IKK(s~A) + TNFR(st~a)    10^k_a
    # inactivation of IKK
    R4: IKK(s~A) -> IKK(s~I)  10^k_4
    # formation of cytoplasmic NFkB-IkB complex 
    R5: NFkB(IkB,loc~c) + IkB(NFkB,loc~c) <-> NFkB(IkB!0,loc~c).IkB(NFkB!0,loc~c)  10^k_a1a, 10^k_d1a 
    # formation of nuclear NFkB-IkB complex --- 3.3*k_a1a is for a volume correction factor 
    R6: NFkB(IkB,loc~n) + IkB(NFkB,loc~n) <-> NFkB(IkB!0,loc~n).IkB(NFkB!0,loc~n)     3.3*(10^k_a1a), 10^k_d1a
    # cytoplasmic free IkB degredation by IKK
    R7: IKK(s~A) + IkB(NFkB,loc~c) -> IKK(s~A)  10^k_t1a
    # cytoplasmic bound IkB degredation by IKK
    R8: IKK(s~A) + NFkB(IkB!0,loc~c).IkB(NFkB!0,loc~c) -> IKK(s~A) + NFkB(IkB,loc~c)     10^k_t2a
    # degradation of basal cytoplasmic free IkB  
    R9: IkB(NFkB,loc~c) -> 0  10^c_4a
    # degradation of basal cytoplasmic bound IkB
    R10: NFkB(IkB!0,loc~c).IkB(NFkB!0,loc~c) -> NFkB(IkB,loc~c)    10^c_5a
    # NFkB shuttling
    R11: NFkB(IkB,loc~c) <-> NFkB(IkB,loc~n)  10^k_i1,10^k_e1
    # IkB shuttling
    R12: IkB(NFkB,loc~c) <-> IkB(NFkB,loc~n) 10^k_i2, 10^k_e2
    # IkB production
    R13: NFkB(IkB,loc~n) -> NFkB(IkB,loc~n) + IkB(NFkB,loc~c)   10^c_1a
    # export of NFkB-IkB from nucleus 
    R14: NFkB(IkB!0,loc~n).IkB(NFkB!0,loc~n) -> NFkB(IkB!0,loc~c).IkB(NFkB!0,loc~c)     10^k_e2a
    # production of A20
    R15: NFkB(IkB,loc~n) -> NFkB(IkB,loc~n) + A20()   10^c_1
    # inactivation of IKK by 20
    R16: A20() + IKK(s~A) -> A20() + IKK(s~I) 10^k_ikk
    # inactivation of TNFR by A20
    R17: A20() + TNFR(st~a) -> A20() + TNFR(st~i) 10^k_tnfr 
    # degredation of A20
    R18: A20() -> 0     10^c_3

end reaction rules

end model


# generate reaction network
generate_network({overwrite=>1})
writeSBML()
writeMfile({})
writeMexfile({})

#Equilibration
simulate_ode({t_start=>0,t_end=>1e8,n_steps=>1000,atol=>1.0E-12,rtol=>1.0E-12})
#Simulate pulse
setConcentration("TNF()",5)
simulate_ode({t_start=>0,t_end=>300,n_steps=>300,atol=>1.0E-12,rtol=>1.0E-12})
#Simulate washout
setConcentration("TNF()",0)
simulate_ode({t_start=>0,t_end=>14100,n_steps=>47,atol=>1.0E-12,rtol=>1.0E-12})

